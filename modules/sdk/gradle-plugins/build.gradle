import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.StripPathSegmentsAction

import de.undercouch.gradle.tasks.download.Download

apply plugin: "de.undercouch.download"
apply plugin: "org.ysb33r.gradletest"

task copyBundle(type: Copy)
task copyGradleTestDependencies(type: Copy)
task downloadBundle(type: Download)

String bundleUrl = "https://cdn.lfrs.sl/releases.liferay.com/portal/7.0.4-ga5/liferay-ce-portal-tomcat-7.0-ga5-20171018150113838.zip"

if (System.properties["http.proxyHost"] == "squid.lax.liferay.com") {
	bundleUrl = "http://mirrors.liferay.com/releases.liferay.com/portal/7.0.4-ga5/liferay-ce-portal-tomcat-7.0-ga5-20171018150113838.zip"
}

copyBundle {
	dependsOn downloadBundle
	eachFile new StripPathSegmentsAction(1)

	from {
		zipTree(downloadBundle.dest)
	}

	includeEmptyDirs = false
	into new File(buildDir, "bundle")
}

copyGradleTestDependencies {
	from configurations.compile
	into jar.destinationDir
}

dependencies {
	compile group: "org.dm.gradle", name: "gradle-bundle-plugin", version: "0.9.0"
	compile project(":sdk:ant-bnd")
	compile project(":sdk:gradle-plugins-alloy-taglib")
	compile project(":sdk:gradle-plugins-css-builder")
	compile project(":sdk:gradle-plugins-db-support")
	compile project(":sdk:gradle-plugins-gulp")
	compile project(":sdk:gradle-plugins-jasper-jspc")
	compile project(":sdk:gradle-plugins-javadoc-formatter")
	compile project(":sdk:gradle-plugins-js-module-config-generator")
	compile project(":sdk:gradle-plugins-js-transpiler")
	compile project(":sdk:gradle-plugins-lang-builder")
	compile project(":sdk:gradle-plugins-service-builder")
	compile project(":sdk:gradle-plugins-source-formatter")
	compile project(":sdk:gradle-plugins-soy")
	compile project(":sdk:gradle-plugins-test-integration")
	compile project(":sdk:gradle-plugins-tld-formatter")
	compile project(":sdk:gradle-plugins-tlddoc-builder")
	compile project(":sdk:gradle-plugins-upgrade-table-builder")
	compile project(":sdk:gradle-plugins-wsdd-builder")
	compile project(":sdk:gradle-plugins-wsdl-builder")
	compile project(":sdk:gradle-plugins-xml-formatter")
	compile project(":sdk:gradle-plugins-xsd-builder")
	compile project(":sdk:gradle-util")

	compileOnly fileTree(builtBy: [rootProject.tasks.extractGradleApi214], dir: new File(rootProject.buildDir, "gradle-2.14"))
}

downloadBundle {
	dest new File(buildDir, "bundle.zip")
	onlyIfModified true
	src bundleUrl
}

gradleTest {
	dependsOn copyBundle
	dependsOn copyGradleTestDependencies
	dependsOn jar
	dependsOn testClasses

	gradleArguments "--project-prop", "app.server.parent.dir=" + FileUtil.getAbsolutePath(copyBundle.destinationDir)

	versions "2.14.1", "3.0", "3.1", "3.2.1", "3.3"
}